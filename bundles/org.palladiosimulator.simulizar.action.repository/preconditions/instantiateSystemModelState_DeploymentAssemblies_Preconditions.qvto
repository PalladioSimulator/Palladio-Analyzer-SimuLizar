/*
 * This QVTO transformation represents the precodition that must be matched, that requires changes
 * in the System Model.
 *
 * The desired state which doesn't require reconfigurations is:
 * 		For each assembled Deployment: 
 * 			The number of instantiated Pods is equal to the replicas definied in the deployment.
 */


import org.palladiosimulator.simulizar.action.repository.black.ProfilesLibrary;
import org.palladiosimulator.simulizar.action.repository.black.SimulationStateLibrary;



modeltype PCM_ALLOC uses 'http://palladiosimulator.org/PalladioComponentModel/Allocation/5.2';
modeltype PCM_REP uses 'http://palladiosimulator.org/PalladioComponentModel/Repository/5.2';
modeltype PCM_RES_ENV uses 'http://palladiosimulator.org/PalladioComponentModel/ResourceEnvironment/5.2';
modeltype PCM_RES_TYPE uses 'http://palladiosimulator.org/PalladioComponentModel/ResourceType/5.2';
modeltype PCM_CORE uses 'http://palladiosimulator.org/PalladioComponentModel/Core/5.2';
modeltype PCM_COMP uses 'http://palladiosimulator.org/PalladioComponentModel/Core/Composition/5.2';
modeltype PCM_ENTITY uses 'http://palladiosimulator.org/PalladioComponentModel/Core/Entity/5.2';
modeltype ACTION_CORE uses 'http://simulizar.palladiosimulator.org/Actions/Core/1.1';
modeltype ACTION_INSTANCE uses 'http://simulizar.palladiosimulator.org/Actions/Instance/1.1';
modeltype PRM uses 'http://simulizar.palladiosimulator.org/RuntimeMeasurement/1.0';
modeltype PCM_SEFF uses 'http://palladiosimulator.org/PalladioComponentModel/SEFF/5.2';
modeltype PCM_SYS uses 'http://palladiosimulator.org/PalladioComponentModel/System/5.2';
modeltype PCM_USAGE uses 'http://palladiosimulator.org/PalladioComponentModel/UsageModel/5.2';
modeltype PCM_FEATURE_CONF uses 'http://sdq.ipd.uka.de/FeatureConfig/2.0';
modeltype PCM_FEATURE_MOD uses 'http://sdq.ipd.uka.de/FeatureModel/2.0';
modeltype PCM_PARAM uses 'http://palladiosimulator.org/PalladioComponentModel/Parameter/5.2';
modeltype PCM_STOEX uses 'http://sdq.ipd.uka.de/StochasticExpressions/2.2';
modeltype PCM_K8S_CONCEPTS uses 'org.palladiosimulator.kubernetesModel.k8sconcepts';
modeltype PCM_K8S_SYSTEM uses 'org.palladiosimulator.kubernetes.kubernetesModel.system';
modeltype PCM_K8S_REPO uses 'org.palladiosimulator.kubernetesModel.repository';
modeltype PCM_K8S_RES_ENV uses 'org.palladiosimulator.kubernetesModel.resourceenvironment';
modeltype PCM_SUBSYS uses 'http://palladiosimulator.org/PalladioComponentModel/SubSystem/5.2';

transformation instantiateSystemModelState_Preconditions (
							inout pcmSystem : PCM_SYS,
							in actionRoleSet : ACTION_INSTANCE,
							in actionStep : ACTION_CORE
							) {

	property systemRoleTypeId : String = '_QQOr9ClWEe6PU9pzUer3wQ';
	property repositoryRoleTypeId : String = '_aq-tVEKhEe6bbaxJhGJDeg';
	property structureDefiningSystemRoleTypeId: String = '_DX79REUVEe6p3-CgWw70fA';
	/**
	 * The main transformation to check the precondition whether the action should be executed.
	 */
	main() {
		log("Check Conditions!");
		assert fatal(pcmSystem.rootObjects()[System]->size() > 0)
			with log("System model is empty!");
		var roleSet : RoleSet := actionRoleSet.rootObjects()[RoleSet]->any(true);
		var roles = roleSet.roles;
		
		var systemRole : instance::Role := roles->select(role | role.roleType.id = systemRoleTypeId)->any(true);
		var system : System := systemRole.value.oclAsType(System);
		
		var repoRole : instance::Role := roles->select(role | role.roleType.id = repositoryRoleTypeId)->any(true);
		var repository : Repository:= repoRole.value.oclAsType(Repository);
		
		var structureDefiningSystemRole : instance::Role := roles->select(role | role.roleType.id = structureDefiningSystemRoleTypeId)->any(true);
		var structureDefiningSystem : System := structureDefiningSystemRole.value.oclAsType(System);
		
		assert fatal(not system.doesSystemStateMatchDesiredState())
			with log("All desired Pods defined in the Deployments of the DeploymentAssemblies are instantiated in the System Model. No reconfiguration required.");
		log("Conditions passed. Pod-Instances have to be added or removed.");
	}
	
	query System::doesSystemStateMatchDesiredState(): Boolean {
		var allInstantiated := true;
    	self.assemblyContexts__ComposedStructure->forEach(context) {
    		if (context.oclIsKindOf(DeploymentAssembly)) {
    			if(not context.oclAsType(DeploymentAssembly).areReplicasInstantiated()) {
    				allInstantiated := false;
    			}
    		}
    	};
		return allInstantiated;
	}
	
	helper DeploymentAssembly::areReplicasInstantiated() :Boolean {
		var replicas = self.deployment.replicas;
		log("Preconditions Replicas: " + replicas.toString());
		var pod : SubSystem = self.deployment.podReference;
		//log("Preconditions Pod Name, ID: " + pod.entityName + ", " + pod.id);
		var parent : ComposedStructure = self.parentStructure__AssemblyContext;
		var instantiatedPodCounter = 0;
		
		parent.assemblyContexts__ComposedStructure->forEach(context) {
			if (context.encapsulatedComponent__AssemblyContext = pod) {
				instantiatedPodCounter := instantiatedPodCounter + 1;
				//log("Preconditions Pod Name, ID: " + context.encapsulatedComponent__AssemblyContext.entityName + ", " + context.encapsulatedComponent__AssemblyContext.id);
			}
		};
		var test := replicas = instantiatedPodCounter;
		log("Currently Instantiated Pod Counter: " + instantiatedPodCounter.toString());
		return test;
	}
}